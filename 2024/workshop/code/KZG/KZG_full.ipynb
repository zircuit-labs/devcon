{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "5e2de468-12c0-4a9e-9cd8-41dc7a637e31",
   "metadata": {},
   "outputs": [],
   "source": [
    "# The BN254 Elliptic curve E(Fp) : y^2 = x^3 + 3 \n",
    "p = 21888242871839275222246405745257275088696311157297823662689037894645226208583\n",
    "Fp = GF(p)\n",
    "E = EllipticCurve(Fp, [0,3])\n",
    "r = E.order()\n",
    "Fr = GF(r)\n",
    "\n",
    "# Field extension of degree 12\n",
    "Fp12.<a> = GF(p^12)\n",
    "RFp12.<T> = PolynomialRing(Fp12)\n",
    "\n",
    "# We define an twist of E over an extension field\n",
    "j = (T^2 + 1).roots(ring=Fp12, multiplicities=0)[0]\n",
    "E2 = EllipticCurve(Fp12, [0,3])\n",
    "E2_twist = EllipticCurve(Fp12, [0, 0x2b149d40ceb8aaae81be18991be06ac3b5b4c5e559dbefa33267e6dc24a138e5 + 266929791119991161246907387137283842545076965332900288569378510910307636690*j])\n",
    "phi = E2_twist.isomorphism_to(E2)\n",
    "\n",
    "# The pairing over E\n",
    "def e(P,Q):\n",
    "    # embedding degree\n",
    "    k = 12\n",
    "    # trace\n",
    "    t = p + 1 - r\n",
    "    return P.ate_pairing(phi(Q), r, k, t, p)\n",
    "\n",
    "# The generators G1, G2 of order r\n",
    "G1 = E2([1, 2])\n",
    "G2 = E2_twist([0x1800deef121f1e76426a00665e5c4479674322d4f75edadd46debd5cd992f6ed+0x198e9393920d483a7260bfb731fb5d25f1aa493335a9e71297e485b7aef312c2*j, 0x12c85ea5db8c6deb4aab71808dcb408fe3d1e7690c43d37b4ce6cc0166fa7daa + 0x090689d0585ff075ec9e99ad690c3395bc4b313370b38ef355acdadcd122975b*j])\n",
    "assert r*G1 == E2(0)\n",
    "assert phi(r*G2) == E2(0)\n",
    "\n",
    "# We check the pairing is a bilinear map\n",
    "assert e(2*G1,3*G2) == e(3*G1,2*G2) == e(1*G1,2*G2)*e(2*G1,2*G2) == e(G1,G2)^6\n",
    "\n",
    "# The polynomial Ring over Fr\n",
    "R.<x> = PolynomialRing(Fr)\n",
    "\n",
    "# Generates a random polynomial of degree less than n\n",
    "def rand_poly(n):\n",
    "    return R([R.base_ring().random_element() for _ in range(n)])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "a49a2d2b-59e3-453d-811f-5e7189813f1d",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Generates a SRS from the curve's generators G1 and G2\n",
    "def setup(n):\n",
    "    s = Fr.random_element()\n",
    "    SRS = [s^i*G1 for i in range(n)]\n",
    "    s_G2 = s*G2\n",
    "    return SRS, s_G2\n",
    "    \n",
    "# Commits to a polynomial\n",
    "def commit(f, SRS):\n",
    "    assert f.degree() < len(SRS)\n",
    "    return sum([ai*Gi for ai, Gi in zip(f.coefficients(), SRS)])\n",
    "\n",
    "# Computes an evaluation proof for f in z\n",
    "def prove(f, z, SRS):\n",
    "    c = f(z)\n",
    "    g = R( (f - c)/ (x-z) )\n",
    "    Cg = commit(g, SRS)\n",
    "    return Cg, c\n",
    "\n",
    "# Verifies an evaluation proof (Cg, z, c) with respect to a commitment Cf\n",
    "def verify(Cf, Cg, c, z, s_G2):\n",
    "    return e(Cf, G2) == e(G1,G2)^c * e(Cg, s_G2-z*G2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "b40f4e1d-561e-4e35-bdd8-98804abc4bf2",
   "metadata": {},
   "outputs": [],
   "source": [
    "# The degree bound\n",
    "n = 100\n",
    "\n",
    "# Setup\n",
    "SRS, s_G2 = setup(n)\n",
    "\n",
    "# We generate a random polynomial\n",
    "f = rand_poly(n)\n",
    "\n",
    "# The Prover commits to f\n",
    "Cf = commit(f, SRS)\n",
    "\n",
    "# The Verifier picks a random evaluation point z and sends it to the Prover\n",
    "z = Fr.random_element()\n",
    "\n",
    "# The Prover computes an evaluation proof for f in z\n",
    "Cg, c = prove(f, z, SRS)\n",
    "\n",
    "# The Verifier checks the evaluation proof\n",
    "assert verify(Cf, Cg, c, z, s_G2)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.3",
   "language": "sage",
   "name": "sagemath-10.3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.11.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
