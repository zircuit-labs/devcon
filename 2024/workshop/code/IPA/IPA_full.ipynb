{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "523e9c06-628e-4d85-a517-58dc0a1f342e",
   "metadata": {},
   "outputs": [],
   "source": [
    "# The BN254 Elliptic curve E(Fp) : y^2 = x^3 + 3 \n",
    "p = 21888242871839275222246405745257275088696311157297823662689037894645226208583\n",
    "Fp = GF(p)\n",
    "E = EllipticCurve(Fp, [0,3])\n",
    "r = E.order()\n",
    "Fr = GF(r)\n",
    "\n",
    "# The polynomial Ring over Fr\n",
    "R.<x> = PolynomialRing(Fr)\n",
    "\n",
    "# Generates a random polynomial of degree less than n\n",
    "def rand_poly(n):\n",
    "    return R([R.base_ring().random_element() for _ in range(n)])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "b22e2c85-933f-4b5d-93b4-61f326933480",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Generates the bases\n",
    "def setup(n): \n",
    "    G = [E.random_element() for i in range(n)]\n",
    "    H = E.random_element()\n",
    "    return G, H\n",
    "\n",
    "# Commits to a polynomial\n",
    "def commit(f, G, H):\n",
    "    assert f.degree() < len(G)\n",
    "    # Generates a random blinding factor bf\n",
    "    bf = Fr.random_element()\n",
    "    Cf = sum([ai*Gi for ai, Gi in zip(f.coefficients(),G)]) + bf*H\n",
    "    return Cf, bf\n",
    "\n",
    "# Executes an interactive protocol showing that a claimed f(z) is correct with respect to a commitment to f\n",
    "def prove_verify_ip(f, z, Cf, bf, n, G, H):\n",
    "    # The prover computes the evaluation f(z) and sends it to the Verifier\n",
    "    c = f(z)\n",
    "\n",
    "    # The Verifier generates a random generator U of E and sends it to the Prover\n",
    "    U = E.random_element()\n",
    "\n",
    "    # The Prover and the Verifier initialize G, P\n",
    "    G = G\n",
    "    P = Cf\n",
    "    \n",
    "    # The Prover initialize also A, B, b\n",
    "    A = f.coefficients() + [Fr(0)]*(n-f.degree()-1)\n",
    "    B = [z^i for i in range(n)]\n",
    "    b = bf\n",
    "\n",
    "    # Prover and Verifier iteratively Split and Fold the input\n",
    "    while len(G) != 1:\n",
    "        \n",
    "        n = len(G)\n",
    "        assert n == len(A) == len(B)\n",
    "\n",
    "        # Prover and Verifier split the vectors G\n",
    "        G_lo, G_hi = G[:n/2], G[n/2:]\n",
    "\n",
    "        # The Prover splits also the vectors A, B\n",
    "        A_lo, A_hi = A[:n/2], A[n/2:]\n",
    "        B_lo, B_hi = B[:n/2], B[n/2:]\n",
    "\n",
    "        # The Prover computes partial commitments L,R from random challenges l,r and sends them to the Verifier\n",
    "        λ = Fr.random_element()\n",
    "        ρ = Fr.random_element()\n",
    "        L = λ*H + sum([a_l*g_h for a_l,g_h in zip(A_lo, G_hi)]) + sum([a_l*b_h for a_l,b_h in zip(A_lo, B_hi)])*U\n",
    "        R = ρ*H + sum([a_h*g_l for a_h,g_l in zip(A_hi, G_lo)]) + sum([a_h*b_l for a_h,b_l in zip(A_hi, B_lo)])*U\n",
    "        \n",
    "        # The Verifier picks a random round challenge x and sends it to the Prover\n",
    "        α = Fr.random_element()\n",
    "    \n",
    "        # Prover and Verifier folds at each round G and updates P\n",
    "        G = [α^-1*g_l + α*g_h for g_l,g_h in zip(G_lo, G_hi)]\n",
    "        P = α^2*L + P + α^-2*R\n",
    "    \n",
    "        # The Prover folds A and B and updates b\n",
    "        A = [α*a_l + α^-1*a_h for a_l,a_h in zip(A_lo, A_hi)]\n",
    "        B = [α^-1*b_l + α*b_h for b_l,b_h in zip(B_lo, B_hi)]\n",
    "        b = α^2*λ + b + α^-2*ρ\n",
    "\n",
    "    # At this point n = 1 and the Prover sends to Verifier the values A[0], B[0], b \n",
    "    # The Verifier ends the protocol by checking if the following equation holds:\n",
    "    return P + c*U == A[0]*G[0] + b*H + A[0]*B[0]*U"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "e0d171b6-403b-447d-bc26-d7453196e6df",
   "metadata": {},
   "outputs": [],
   "source": [
    "# The degree bound\n",
    "k = 8\n",
    "n = 1 << k\n",
    "\n",
    "# Setup\n",
    "G, H = setup(n)\n",
    "\n",
    "# We generate a random polynomial\n",
    "f = rand_poly(n)\n",
    "\n",
    "# The Prover commits to f\n",
    "Cf, bf = commit(f, G, H)\n",
    "\n",
    "# The Verifier picks a random evaluation point z and sends it to the Prover\n",
    "z = Fr.random_element()\n",
    "\n",
    "# The Prover and the Verifier engage in an interactive protocol showing that the claimed f(z) is correct with respect to Cf\n",
    "assert prove_verify_ip(f, z, Cf, bf, n, G, H)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.3",
   "language": "sage",
   "name": "sagemath-10.3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.11.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
